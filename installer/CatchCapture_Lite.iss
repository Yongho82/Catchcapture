; Script generated by Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "CatchCapture"
#define MyAppVersion "1.0.1"
#define MyAppPublisher "Yongho"
#define MyAppExeName "CatchCapture.exe"

[Setup]
AppId={{A5A0FAF0-6D1F-4F4E-B2A6-9B8F0F5D2E31}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
; 기본 폴더는 사용자 문서
DefaultDirName={userdocs}\{#MyAppName}
DisableProgramGroupPage=yes
; 경량화 버전 (런타임 설치 제외로 낮은 권한으로도 설치 가능하게 설정)
PrivilegesRequired=lowest
OutputDir=Output
OutputBaseFilename=CatchCapture_Lite_Setup
SetupIconFile=..\icons\catcha.ico
Compression=lzma2/ultra64
SolidCompression=yes
WizardStyle=modern

; 언어 설정 선택 가능하게 설정
ShowLanguageDialog=yes
; 시스템 언어를 감지하되, 일치하는게 없으면 목록의 첫 번째(영어)를 기본값으로 사용
LanguageDetectionMethod=uilanguage

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "schinese"; MessagesFile: "Languages\ChineseSimplified.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone
Name: "startmenu"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; 1. 메인 프로그램 파일들 (Release Publish 폴더 기준, ffmpeg.exe 제외)
Source: "..\publish_folder\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "ffmpeg.exe"

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
; 프로그램 실행
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{app}\*"

[Code]
// 기존 버전 삭제 로직 (업그레이드 시 충돌 방지)
function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstPathKey: String;
begin
  sUnInstPath := '';
  sUnInstPathKey := 'Software\Microsoft\Windows\CurrentVersion\Uninstall\{{A5A0FAF0-6D1F-4F4E-B2A6-9B8F0F5D2E31}}_is1';
  
  if RegQueryStringValue(HKCU, sUnInstPathKey, 'UninstallString', sUnInstPath) then
    Result := sUnInstPath
  else if RegQueryStringValue(HKLM, sUnInstPathKey, 'UninstallString', sUnInstPath) then
    Result := sUnInstPath
  else
    Result := '';
end;

function InitializeSetup(): Boolean;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
  Result := True;
  sUnInstallString := GetUninstallString();
  
  if sUnInstallString <> '' then
  begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    Exec(sUnInstallString, '/SILENT /NORESTART /SUPPRESSMSGBOXES', '', SW_HIDE, ewWaitUntilTerminated, iResultCode);
  end;
end;
